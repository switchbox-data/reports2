all: clean render

# Fetch People's Gas project data from ArcGIS (downloads to utils/)
fetch-data:
  uv run python utils/download_peoplesgas_data.py

# Fetch Cook County parcels (Chicago only, downloads to data/geo_data/)
fetch-parcels:
  uv run python utils/download_cookcounty_parcels.py

# Fetch Chicago building footprints (downloads to data/geo_data/)
fetch-buildings:
  uv run python utils/download_chicago_buildings.py

# Fetch Chicago street centerlines (downloads to data/geo_data/)
fetch-streets:
  uv run python utils/download_chicago_streets.py

# Fetch all geospatial data (Peoples Gas, parcels, buildings, streets)
fetch-all-data:
  uv run python utils/download_peoplesgas_data.py
  uv run python utils/download_cookcounty_parcels.py
  uv run python utils/download_chicago_buildings.py
  uv run python utils/download_chicago_streets.py

# Explore available feature categories and statistics
explore-data:
  uv run python utils/explore_features.py

# Upload GeoJSON to public S3 bucket (s3://data.sb/gis/pgl/)
upload-data:
  uv run python utils/upload_to_s3.py

# Delete all caches
clean:
  rm -rf .quarto docs/*_files/ notebooks/*_files/ notebooks/*.html \
         notebooks/*.ipynb notebooks/*.rmarkdown

# Render quarto project
render:
  quarto render .

today := `date +%Y%m%d`
draft_name := "report_draft_" + today + ".docx"
draft:
  quarto render index.qmd --to docx --output {{draft_name}}

typeset_name := "report_" + today + ".icml"
typeset:
  quarto render index.qmd --to icml --output {{typeset_name}}

publish:
  bash -c 'parent_folder=$(basename $(pwd)); pub_path="../../docs/$parent_folder"; \
  rm -rf "$pub_path" && mkdir -p "$pub_path" && cp -r docs/* "$pub_path"'
